#/ This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
#// Â© MBCryptocurrency
#indicator("Supply and Demand Zone Confirmation",  overlay=true, timeframe="", timeframe_gaps=false)
# Converted and mod by Sam4Cok@samer800 - 01/2023

#//CCI
input ShowLabel  = yes;
input cci1Timeframe = AggregationPeriod.DAY;      # 'First CCI timeframe'
input cci2Timeframe = AggregationPeriod.WEEK; # 'second CCI timeframe'
input rsiTimeframe = AggregationPeriod.DAY;
input MovAvgType = AverageType.SIMPLE;

def na = Double.NaN;

def hlcP1  = hlc3(Period = cci1Timeframe);
def hlcP2 = hlc3(Period = cci2Timeframe);

def ma = MovingAverage(MovAvgType, hlc3, 14);
def cci = (hlc3 - ma) / (0.015 * LinDev(hlc3, 20));

def maP1 = MovingAverage(MovAvgType, hlcP1, 14);
def cci6 = (hlcP1 - maP1) / (0.015 * LinDev(hlcP1, 20));

def maP2 = MovingAverage(MovAvgType, hlcP2, 14);
def cciP2 = (hlcP2 - maP2) / (0.015 * LinDev(hlcP2, 20));

def CCI15 = cci6;
def CCIP1 = cciP2;

#///RSI

def change = close - close[1];
def changeP1 = close(Period=RSItimeframe) - close(Period=RSItimeframe)[1];

def up   = WildersAverage(max(change, 0), 14);
def down = WildersAverage(-min(change, 0), 14);
def rsi = if down == 0 then 100 else
          if up == 0 then 0 else 100 - (100 / (1 + up / down));
def rsiMA = MovingAverage(MovAvgType, rsi, 14);

def upP1   = WildersAverage(max(changeP1, 0), 14);
def downP1 = WildersAverage(-min(changeP1, 0), 14);
def rsiP1  = if downP1 == 0 then 100 else
             if upP1 == 0 then 0 else 100 - (100 / (1 + upP1 / downP1));
def rsi15 = rsiP1;

#//condition
def cross  = crosses(cci,-100);
def cross2 = crosses(cci,100);

def signal_buy;# = false
def signal_sell;# = false

if  CCI15 < -100 and CCIP1 < -150 and rsiMA < 35 and rsi15 < 50 {
    signal_buy = yes;
    signal_sell = no;
    } else
if  CCI15 > 100 and CCIP1 > 150 and rsiMA > 65 and rsi15 > 50 {
    signal_buy = no;
    signal_sell = yes;
    } else {
    signal_buy = no;
    signal_sell = no;
}
#//only first signal to show
def Buy;# = false
def Sell;# = false

Buy  = if signal_buy then yes else
       if signal_sell then no else if cross then no else if cross2 then no else Buy[1];
Sell = if signal_sell then yes else
       if signal_buy then no else if cross then no else if cross2 then no else Sell[1];

def Final_buy  = signal_buy and !Buy[1];
def Final_sell = signal_sell and !Sell[1];

#--- Lines
def buyLineL = if Final_buy then low[1] else buyLineL[1];
def buyLineH = if Final_buy then high[1] else buyLineH[1];
def SellLineH = if Final_sell then high[1] else SellLineH[1];
def SellLineL = if Final_sell then low[1] else SellLineL[1];

plot SupLineL  = if buyLineL==buyLineL[1] then buyLineL else na;
plot SupLineH  = if buyLineH==buyLineH[1] then buyLineH else na;
plot ResLineH  = if SellLineH==SellLineH[1] then SellLineH else na;
plot ResLineL  = if SellLineL==SellLineL[1] then SellLineL else na;

SupLineL.setHiding(yes);
SupLineH.SetHiding(yes);
ResLineH.SetHiding(yes);
ResLineL.SetHiding(yes);

AddCloud(SupLineH, SupLineL, Color.DARK_GREEN, Color.DARK_GREEN, yes);
AddCloud(ResLineH, ResLineL, Color.DARK_RED, Color.DARK_RED, yes);
#//plotshapes

AddChartBubble(ShowLabel and Final_sell, high, "Sell Zone", Color.RED, yes);
AddChartBubble(ShowLabel and Final_buy, low, "Buy Zone", Color.GREEN, no);



#--- END CODE